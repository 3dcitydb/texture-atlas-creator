import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'org.citydb'
version '1.4.0'
description 'Library for creating texture atlases for the 3D City Database'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

ext {
    date = new Date()
}

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
    into('META-INF') {
        from 'LICENSE'
        from('resources/doc/README.txt') {
            filter(ReplaceTokens, tokens: [
                    version: project.version,
                    date: date.format('yyyy-MM-dd'),
                    copyright: date.format('yyyy')
            ])
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/3dcitydb'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'clausnagel'
                        name = 'Claus Nagel'
                        email = 'claus.nagel@gmail.com'
                        roles = ['architect', 'developer']
                        timezone = '+1'
                    }
                }
                scm {
                    url = 'https://github.com/3dcitydb/texture-atlas-creator'
                    connection = 'scm:git:https://github.com/3dcitydb/texture-atlas-creator.git'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        userOrg = '3dcitydb'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/3dcitydb'
        vcsUrl = 'scm:git:https://github.com/3dcitydb/texture-atlas-creator.git'
        issueTrackerUrl = 'https://github.com/3dcitydb/texture-atlas-creator/issues'
        licenses = ['Apache-2.0']
        version {
            name = project.version
        }
    }
}